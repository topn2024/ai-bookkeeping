pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.9.1" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

include ":app"

gradle.beforeProject { project ->
    project.afterEvaluate {
        if (project.hasProperty('android') && project.android.namespace == null) {
            def manifest = file("${project.projectDir}/src/main/AndroidManifest.xml")
            if (manifest.exists()) {
                def packageName = new groovy.xml.XmlSlurper().parse(manifest).@package.toString()
                if (packageName) {
                    project.android.namespace = packageName
                }
            }
        }
        // Set Kotlin JVM target for all subprojects
        if (project.hasProperty('android')) {
            project.android {
                if (it.hasProperty('kotlinOptions')) {
                    it.kotlinOptions {
                        jvmTarget = '17'
                    }
                }
                if (it.hasProperty('compileOptions')) {
                    it.compileOptions {
                        sourceCompatibility = JavaVersion.VERSION_17
                        targetCompatibility = JavaVersion.VERSION_17
                    }
                }
            }
        }
    }
}
